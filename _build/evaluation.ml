(* 
                         CS 51 Final Project
                         MiniML -- Evaluation
*)

(* This module implements a small untyped ML-like language under
   various operational semantics.
 *)

open Expr ;;
  
(* Exception for evaluator runtime, generated by a runtime error in
   the interpreter *)
exception EvalError of string ;;
  
(* Exception for evaluator runtime, generated by an explicit `raise`
   construct in the object language *)
exception EvalException ;;

(*......................................................................
  Environments and values 
 *)

module type ENV = sig
    (* the type of environments *)
    type env
    (* the type of values stored in environments *)
    type value =
      | Val of expr
      | Closure of (expr * env)
   
    (* empty () -- Returns an empty environment *)
    val empty : unit -> env

    (* close expr env -- Returns a closure for `expr` and its `env` *)
    val close : expr -> env -> value

    (* lookup env varid -- Returns the value in the `env` for the
       `varid`, raising an `Eval_error` if not found *)
    val lookup : env -> varid -> value

    (* extend env varid loc -- Returns a new environment just like
       `env` except that it maps the variable `varid` to the `value`
       stored at `loc`. This allows later changing the value, an
       ability used in the evaluation of `letrec`. To make good on
       this, extending an environment needs to preserve the previous
       bindings in a physical, not just structural, way. *)
    val extend : env -> varid -> value ref -> env

    (* env_to_string env -- Returns a printable string representation
       of environment `env` *)
    val env_to_string : env -> string
                                 
    (* value_to_string ?printenvp value -- Returns a printable string
       representation of a value; the optional flag `printenvp`
       (default: `true`) determines whether to include the environment
       in the string representation when called on a closure *)
    val value_to_string : ?printenvp:bool -> value -> string
  end

module Env : ENV =
  struct
    type env = (varid * value ref) list
     and value =
       | Val of expr
       | Closure of (expr * env)

    let empty () : env = []

    let close (exp : expr) (env : env) : value = Closure (exp, env)

    let lookup (env : env) (varname : varid) : value =
      try !(List.assoc varname env)
        with Not_found -> raise (EvalError (varname ^ "not in env"))

    let extend (env : env) (varname : varid) (loc : value ref) : env =
      (varname, loc) :: (List.remove_assoc varname env) 

    let rec value_to_string ?(printenvp : bool = true) (v : value) : string =
      match v with
      | Closure (x, v) -> if printenvp then  exp_to_concrete_string x ^
                            " in env: " ^ env_to_string v
                          else exp_to_concrete_string x
      | Val a -> exp_to_concrete_string a

    and env_to_string (env : env) : string =
      List.fold_left (fun x (a, b) -> 
        x ^ "(" ^ a ^ ", " ^ value_to_string !b ^ "), ") "" env
  end
;;

(*......................................................................
  Evaluation functions

  Each of the evaluation functions below evaluates an expression `exp`
  in an environment `env` returning a result of type `value`. We've
  provided an initial implementation for a trivial evaluator, which
  just converts the expression unchanged to a `value` and returns it,
  along with "stub code" for three more evaluators: a substitution
  model evaluator and dynamic and lexical environment model versions.

  Each evaluator is of type `expr -> Env.env -> Env.value` for
  consistency, though some of the evaluators don't need an
  environment, and some will only return values that are "bare
  values" (that is, not closures). 

  DO NOT CHANGE THE TYPE SIGNATURES OF THESE FUNCTIONS. Compilation
  against our unit tests relies on their having these signatures. If
  you want to implement an extension whose evaluator has a different
  signature, implement it as `eval_e` below.  *)

(* The TRIVIAL EVALUATOR, which leaves the expression to be evaluated
   essentially unchanged, just converted to a value for consistency
   with the signature of the evaluators. *)
   
let eval_t (exp : expr) (_env : Env.env) : Env.value =
  (* coerce the expr, unchanged, into a value *)
  Env.Val exp ;;

(* The SUBSTITUTION MODEL evaluator -- to be completed *)

let binop_eval (bin : binop) (e1 : expr) (e2 : expr) : expr = 
  match bin, e1, e2 with 
  | LessThan, Num a, Num b -> Bool (a < b)
  | Equals, Bool a, Bool b -> Bool (a && b)
  | IPlus, Num a, Num b -> Num (a + b)
  | IMinus, Num a, Num b -> Num (a - b)
  | ITimes, Num a, Num b -> Num (a * b)
  | FPlus, Float a, Float b -> Float (a +. b)
  | FMinus, Float a, Float b -> Float (a -. b)
  | FTimes, Float a, Float b -> Float (a *. b)
  | Equals, Num a, Num b -> Bool (a = b)
  | Or, Bool a, Bool b -> Bool (a || b)
  | And, Bool a, Bool b -> Bool (a && b)
  | _, _, _ -> raise (EvalError "Invalid binop") ;;

let unop_eval (op : unop) (e : expr) : expr = 
  match op, e with 
  | Negate, Num a -> Num (~-a)
  | _, _ -> raise (EvalError "Invalid unop") ;;
   
let eval_s (exp : expr) (_env : Env.env) : Env.value =
  let rec eval_s_helper (exp : expr) : expr =
    match exp with 
    | Num _ | Bool _ | Float _ | Unassigned | Raise | Fun (_, _) -> exp
    | Var _ -> raise (EvalError "Unbound var")
    | Unop (op, e) -> unop_eval op (eval_s_helper e)
    | Binop (op, e1, e2) -> binop_eval op (eval_s_helper e1) (eval_s_helper e2)
    | Let (v, e1, e2) -> eval_s_helper (subst v (eval_s_helper e1) e2)
    | Conditional (e1, e2, e3) -> (match eval_s_helper e1 with 
                                  | Bool a -> if a then eval_s_helper e2 
                                              else eval_s_helper e3 
                                  | _ -> raise (EvalError "Invalid conditional"))
    | Letrec (v, e1, e2) -> eval_s_helper (subst v (eval_s_helper (subst v 
                                        (Letrec (v, e1, Var v)) e1)) e2)
    | App (e1, e2) -> (match eval_s_helper e1 with 
                      | Fun (v, e) -> eval_s_helper 
                                      (subst v (eval_s_helper e2) e)
                      | _ -> raise (EvalError "Invalid App"))
  in Val (eval_s_helper exp) ;;
   
let eval_d_l (exp : expr) (env : Env.env) (dynamic : bool): Env.value =
  let rec eval_d_l_helper (exp: expr) (env : Env.env) : Env.value = 
    match exp with 
    | Num _ | Float _ | Bool _ | Unassigned | Raise -> Env.Val exp
    | Var a -> Env.lookup env a
    | Fun _ -> if dynamic then Env.Val exp else Env.close exp env
    | Unop (op, e1) -> (match eval_d_l_helper e1 env with 
                       | Env.Val e2 -> Env.Val (unop_eval op e2)
                       | _ -> raise (EvalError "invalid unop"))
    | Binop (op, e1, e2) -> (match eval_d_l_helper e1 env, 
                             eval_d_l_helper e2 env with
                             | Env.Val e1, Env.Val e2 -> 
                                Env.Val (binop_eval op e1 e2)
                             | _, _ -> raise (EvalError "invalid binop"))
    | Let (v, e1, e2) -> eval_d_l_helper (App (Fun (v, e2), e1)) env
    | Conditional (e1, e2, e3) -> (match eval_d_l_helper e1 env with 
                                   | Env.Val (Bool a) -> 
                                      if a then eval_d_l_helper e2 env 
                                      else eval_d_l_helper e3 env
                                   | _ -> raise (EvalError "Invalid conditional"))
    | Letrec (v, e1, e2) -> let valt = ref (Env.Val Unassigned) in 
                            let new_env = Env.extend env v valt in 
                            let new_valt = ref (eval_d_l_helper e1 new_env) in
                            (valt := !new_valt; eval_d_l_helper e2 
                            (Env.extend env v valt))
    | App (e1, e2) -> match eval_d_l_helper e1 env with
                      | Env.Val (Fun(v, e)) -> 
                          if dynamic then eval_d_l_helper e 
                          (Env.extend env v (ref (eval_d_l_helper e2 env))) 
                          else raise EvalException
                      | Closure (Fun (v, e), ev) -> 
                          if dynamic then raise EvalException 
                          else eval_d_l_helper e 
                          (Env.extend ev v (ref (eval_d_l_helper e2 env)))
                      | _ -> raise (EvalError "Invalid App")
  in eval_d_l_helper exp env ;;

(* The DYNAMICALLY-SCOPED ENVIRONMENT MODEL evaluator -- to be
   completed *)
let eval_d (exp : expr) (env : Env.env) : Env.value =
  eval_d_l exp env true ;;
   
(* The LEXICALLY-SCOPED ENVIRONMENT MODEL evaluator -- optionally
   completed as (part of) your extension *)
let eval_l (exp : expr) (env : Env.env) : Env.value =
  eval_d_l exp env false ;;

(* The EXTENDED evaluator -- if you want, you can provide your
   extension as a separate evaluator, or if it is type- and
   correctness-compatible with one of the above, you can incorporate
   your extensions within `eval_s`, `eval_d`, or `eval_l`. *)

let _eval_e _ =
  failwith "eval_e not implemented" ;;
  
(* Connecting the evaluators to the external world. The REPL in
   `miniml.ml` uses a call to the single function `evaluate` defined
   here. Initially, `evaluate` is the trivial evaluator `eval_t`. But
   you can define it to use any of the other evaluators as you proceed
   to implement them. (We will directly unit test the four evaluators
   above, not the `evaluate` function, so it doesn't matter how it's
   set when you submit your solution.) *)
   
let evaluate = eval_l ;;


